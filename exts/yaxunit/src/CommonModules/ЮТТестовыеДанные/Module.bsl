//©///////////////////////////////////////////////////////////////////////////©//
//
//  Copyright 2021-2023 BIA-Technologies Limited Liability Company
//
//  Licensed under the Apache License, Version 2.0 (the "License");
//  you may not use this file except in compliance with the License.
//  You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
//  Unless required by applicable law or agreed to in writing, software
//  distributed under the License is distributed on an "AS IS" BASIS,
//  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
//  See the License for the specific language governing permissions and
//  limitations under the License.
//
//©///////////////////////////////////////////////////////////////////////////©//

#Область ПрограммныйИнтерфейс

// Создает новый элемент и возвращает его ссылку.
//  
// Параметры:
//  Менеджер - Произвольный - Менеджер справочника/ПВХ и тд.
//  Наименование - Строка, Неопределено - Наименование элемента
//  Реквизиты - Структура, Неопределено - Значения реквизитов элемента
// 
// Возвращаемое значение:
//  ЛюбаяСсылка - Ссылка на созданный объект
Функция СоздатьЭлемент(Менеджер, Наименование = Неопределено, Реквизиты = Неопределено) Экспорт
	
	Возврат ЮТТестовыеДанныеВызовСервера.СоздатьЭлемент(Менеджер, Наименование, Реквизиты);
	
КонецФункции

// Создает новый документ и возвращает его ссылку.
//  
// Параметры:
//  Менеджер - Произвольный - Менеджер справочника/ПВХ и тд.
//  Реквизиты - Структура, Неопределено - Значения реквизитов элемента
// 
// Возвращаемое значение:
//  ДокументСсылка - Ссылка на созданный объект
Функция СоздатьДокумент(Менеджер, Реквизиты = Неопределено) Экспорт
	
	Возврат ЮТТестовыеДанныеВызовСервера.СоздатьДокумент(Менеджер, Реквизиты);
	
КонецФункции

// Генерирует и возвращает случайное число.
// 
// Параметры:
//  Минимум - Неопределено, Число - Минимальное значение
//  Максимум - Неопределено, Число - Максимальное значение
//  ЗнаковПослеЗапятой - Число - Количество знаков после запятой
// 
// Возвращаемое значение:
//  Число - Случайное число
Функция СлучайноеЧисло(Минимум = 0, Максимум = Неопределено, ЗнаковПослеЗапятой = 0) Экспорт
	
#Если ВебКлиент Тогда
	ВызватьИсключение ЮТОбщий.МетодНеДоступен("ЮТТестовыеДанные.СлучайноеЧисло");
#Иначе
	Генератор = ЮТКонтекст.ЗначениеКонтекста("ГенераторСлучайныхЧисел");
	
	Если Генератор = Неопределено Тогда
		Генератор = Новый ГенераторСлучайныхЧисел();
		ЮТКонтекст.УстановитьЗначениеКонтекста("ГенераторСлучайныхЧисел", Генератор);
	КонецЕсли;
	
	Если Максимум = Неопределено Тогда
		Результат = Генератор.СлучайноеЧисло(Минимум);
	Иначе
		Результат = Генератор.СлучайноеЧисло(Минимум, Максимум);
	КонецЕсли;
	
	Если ЗнаковПослеЗапятой > 0 Тогда
		Множитель = Pow(10, ЗнаковПослеЗапятой);
		Результат = Результат + Окр(Генератор.СлучайноеЧисло(0, Множитель) / Множитель, ЗнаковПослеЗапятой);
	КонецЕсли;
	
	Возврат Результат;
#КонецЕсли
	
КонецФункции

// Генерирует и возвращает случайное положительное число.
// 
// Параметры:
//  Максимум - Неопределено, Число - Максимальное значение
//  ЗнаковПослеЗапятой - Число - Знаков после запятой
// 
// Возвращаемое значение:
//  Число - Случайное положительное число
Функция СлучайноеПоложительноеЧисло(Максимум = Неопределено, ЗнаковПослеЗапятой = 0) Экспорт
	
	Возврат СлучайноеЧисло(1, Максимум, ЗнаковПослеЗапятой);
	
КонецФункции

// Генерирует и возвращает случайное отрицательное число.
// 
// Параметры:
//  Минимум - Неопределено, Число - Минимальное значение
//  ЗнаковПослеЗапятой - Число - Знаков после запятой
// 
// Возвращаемое значение:
//  Число - Случайное отрицательное число
Функция СлучайноеОтрицательноеЧисло(Минимум = Неопределено, ЗнаковПослеЗапятой = 0) Экспорт
	
	Возврат -СлучайноеЧисло(0, -Минимум, ЗнаковПослеЗапятой);
	
КонецФункции

// Генерирует и возвращает случайную строку указанной длины, строка может содержать цифры, английские и русские буквы в разных регистрах.
// 
// Параметры:
//  Длина - Число - Длина генерируемой строки, без учета префикса
//  Префикс - Строка - Префикс строки
// 
// Возвращаемое значение:
//  Строка - Случайная строка
Функция СлучайнаяСтрока(Знач Длина = 10, Префикс = "") Экспорт
	
	Строка = "1234567890абвгдеёжзийклмнопрстуфхцчшщъыьэюяАБВГДЕЁЖЗИЙКЛМНОПРСТУФХЦЧШЩЪЫЬЭЮЯABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz";
	
	Результат = "";
	КоличествоСимволов = СтрДлина(Строка);
	
	Длина = Длина - СтрДлина(Префикс);
	
	Для Инд = 1 По Длина Цикл
		
		Результат = Результат + Сред(Строка, СлучайноеЧисло(1, КоличествоСимволов), 1);
		
	КонецЦикла;
	
	Возврат Префикс + Результат;
	
КонецФункции

// Генерирует и возвращает случайную дату в указанном интервале (если не указан используется `0001-01-01  - 3999-12-31`).
// 
// Параметры:
//  Минимум - Дата - Минимальное значение случайной даты
//          - Неопределено - Если не указано используется `0001-01-01`
//  Максимум - Дата - Максимальное значение случайной даты
//           - Неопределено - Если не указано используется `3999-12-31`
// 
// Возвращаемое значение:
//  Дата - Случайная дата
Функция СлучайнаяДата(Знач Минимум = Неопределено, Знач Максимум = Неопределено) Экспорт
	
	Если Минимум = Неопределено Тогда
		Минимум = '00010101';
	КонецЕсли;
	
	Если Максимум = Неопределено Тогда
		Максимум = '39991231';
	КонецЕсли;
	
	СекундВДне = 86400;
	КоличествоДней = Цел((Максимум - Минимум) / СекундВДне);
	Возврат Минимум + СлучайноеЧисло(0, КоличествоДней) * СекундВДне + СлучайноеЧисло(0, СекундВДне);
	
КонецФункции

// Генерирует и возвращает случайный IP адрес.
// 
// Возвращаемое значение:
//  Строка - Случайный IP адрес
Функция СлучайныйIPАдрес() Экспорт
	
	Части = Новый Массив();
	Части.Добавить(СлучайноеЧисло(1, 253));
	Части.Добавить(СлучайноеЧисло(1, 253));
	Части.Добавить(СлучайноеЧисло(1, 253));
	Части.Добавить(СлучайноеЧисло(1, 253));
	
	Возврат СтрСоединить(Части, ".");
	
КонецФункции

// Генерирует и возвращает уникальную строку, формируется из уникального идентификатора.
// 
// Параметры:
//  Префикс - Строка - Префикс строки
// 
// Возвращаемое значение:
//  Строка - Уникальная строка
Функция УникальнаяСтрока(Префикс = "") Экспорт
	
	Возврат Префикс + Новый УникальныйИдентификатор();
	
КонецФункции

// Возвращает случайный элемент списка.
// 
// Параметры:
//  Список - Массив из Произвольный - Коллекция возможных значений
// 
// Возвращаемое значение:
//  Произвольный - случайное значение из списка
Функция СлучайноеЗначениеИзСписка(Список) Экспорт
	
	Индекс = СлучайноеЧисло(0, Список.ВГраница());
	
	Возврат Список[Индекс];
	
КонецФункции

// Возвращает случайно логическое значение.
// 
// Возвращаемое значение:
//  Булево - Случайное булево
Функция СлучайноеБулево() Экспорт
	
	Возврат СлучайноеЧисло() %2 = 0;
	
КонецФункции

// Создает новый файл, который будет удален после теста
// 
// Параметры:
//  Содержимое - Строка, Неопределено - Содержимое файла
//  ТолькоЧтение - Булево - Установить атрибут `только чтение`
//  Расширение - Строка, Неопределено - Расширение нового файла
// 
// Возвращаемое значение:
//  Строка - Новый файл
Функция НовыйФайл(Содержимое = Неопределено, ТолькоЧтение = Ложь, Расширение = Неопределено) Экспорт
	
	Результат = НовоеИмяВременногоФайла(Расширение);
	
	ЗаписьДанных = Новый ЗаписьДанных(Результат);
	
	Если Содержимое <> Неопределено Тогда
		ЗаписьДанных.ЗаписатьСимволы(Содержимое);
	КонецЕсли;
	
	ЗаписьДанных.Закрыть();
	
	Если ТолькоЧтение Тогда
		СозданныйФайл = Новый Файл(Результат);
		СозданныйФайл.УстановитьТолькоЧтение(Истина);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает имя нового файла.
// По окончании выполнения теста этот файл будет удален.
// 
// Параметры:
//  Расширение - Строка - Расширение нового файла
// 
// Возвращаемое значение:
//  Строка
Функция НовоеИмяВременногоФайла(Расширение = Неопределено) Экспорт
	
	Результат = ПолучитьИмяВременногоФайла(Расширение);
	ЮТКонтекст.КонтекстТеста().ВременныеФайлы.Добавить(Результат);
	Возврат Результат;
	
КонецФункции

// Читает таблицу MarkDown в массив структур
// 
// Параметры:
//  Строки - Строка - Таблица markdown
// 
// Возвращаемое значение:
//  Массив  из Структура - Данные таблицы markdown
Функция ТаблицаMarkDown(Строки) Экспорт
	
	ЗагрузилиЗаголовок = Ложь;
	Результат = Новый Массив();
	Ключи = "";
	
	Разделитель = "|";
	
	Кодировка = КодировкаТекста.UTF8;
	Поток = ПолучитьДвоичныеДанныеИзСтроки(Строки, Кодировка).ОткрытьПотокДляЧтения();
	Чтение = Новый ЧтениеТекста(Поток, Кодировка);
	
	Пока Истина Цикл
		
		Строка = Чтение.ПрочитатьСтроку();
		Если Строка = Неопределено Тогда
			Прервать;
		КонецЕсли;
		
		Строка = СокрЛП(Строка);
		
		Если ПустаяСтрока(Строка) Тогда
			Продолжить;
		ИначеЕсли НЕ СтрНачинаетсяС(Строка, Разделитель) Тогда
			Если ЗагрузилиЗаголовок Тогда
				Прервать;
			Иначе
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		Блоки = СтрРазделить(Строка, Разделитель);
		
		Если ЗагрузилиЗаголовок Тогда
			
			Если Блоки.Количество() <> Ключи.Количество() Тогда
				ВызватьИсключение СтрШаблон("Количество значений в строке (%1) Markdown не совпадает с количеством заголовков (2):
											|%3", Блоки.Количество(), Ключи.Количество(), Строка);
			КонецЕсли;
			
			СтрокаРезультата = Новый Структура();
			Для Инд = 1 По Блоки.ВГраница() - 1 Цикл
				СтрокаРезультата.Вставить(Ключи[Инд], СокрЛП(Блоки[Инд]));
			КонецЦикла;
			Результат.Добавить(СтрокаРезультата);
		Иначе
			Ключи = Новый Массив();
			Для Инд = 0 По Блоки.ВГраница() Цикл
				Ключи.Добавить(СокрЛП(Блоки[Инд]));
			КонецЦикла;
			Чтение.ПрочитатьСтроку(); // Пропуск строки разделителя
			ЗагрузилиЗаголовок = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Чтение.Закрыть();
	Поток.Закрыть();
	
	Возврат Результат;
	
КонецФункции

// Формирует структуру на основании таблицы Markdown
// 
// Параметры:
//  Ключ - Строка - Имя ключевой колонки
//  Строки - Строка - Таблица markdown
// 
// Возвращаемое значение:
//  Структура
Функция СтруктураMarkDown(Ключ, Строки) Экспорт
	
	Таблица = ТаблицаMarkDown(Строки);
	
	Результат = Новый Структура();
	
	Для Каждого Строка Из Таблица Цикл
		Результат.Вставить(Строка[Ключ], Строка);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Возвращает конструктор создания тестовых данных
// 
// Конструктор имеет ряд особенностей:
// 
// * Нельзя использовать параллельно несколько конструкторов. 
//   Например
//   
//   ```bsl
//   Пользователь = КонструкторОбъекта(Справочники.Пользователи);
//   Документ = КонструкторОбъекта(Документы.Приход);
//   ...
//   Пользователь.Записать();
//   Документ.Провести();
//   ```
//   
// * Создание объекта происходит при вызове методов `Записать` и `Провести`, а создание реквизитов происходит во время вызова методов установки.
// * При использовании на клиенте все значения должны быть сериализуемыми.
// 
// Параметры:
//  Менеджер - Строка - Имя менеджера. Примеры: Справочники.Товары, Документы.ПриходТоваров
// 
// Возвращаемое значение:
//  ОбщийМодуль - Конструктор
Функция КонструкторОбъекта(Менеджер) Экспорт
	
	Возврат ЮТКонструкторТестовыхДанных.Инициализировать(Менеджер);
	
КонецФункции

// Удаляет переданные объекта
// 
// Параметры:
//  Ссылки - Массив из ЛюбаяСсылка
Процедура Удалить(Ссылки) Экспорт
	
	ЮТТестовыеДанныеВызовСервера.Удалить(Ссылки);
	
КонецПроцедуры

#Если Сервер Тогда
// Возвращает мок для `HTTPСервисЗапрос`.
// 
// Возвращаемое значение:
//  ОбработкаОбъект.ЮТHTTPСервисЗапрос - Мок
Функция HTTPСервисЗапрос() Экспорт
	
	Возврат Обработки.ЮТHTTPСервисЗапрос.Создать();
	
КонецФункции
#КонецЕсли


#Область Бизнес_данные

// Формирует случайный валидный ИНН РФ.
// 
// Параметры:
//  КодРегиона - Строка - Код региона это первые две цифры кода ИНН. Список действующих кодов регионов
//    можно подсмотреть: https://www.nalog.gov.ru/html/docs/kods_regions.doc . Если код региона задан
//    как "00" то будет сформирован случайный код региона. По умолчанию "00" (случайный код региона) 
//  ЭтоИННФизическогоЛица - Булево - Если Истина, то это ИНН физического лица, иначе ИНН юридического
//    лица. По умолчанию Ложь (ИНН юридического лица)
// 
// Возвращаемое значение:
//  Строка - Случайный ИНН
Функция СлучайныйИНН(КодРегиона = "00", ЭтоИННФизическогоЛица = Ложь) Экспорт

	СлучайныйИНН = "";

	Если ЭтоИННФизическогоЛица Тогда
		ИННМассив = Новый Массив(12);
		ВесовыеКоэффициенты1 = СтрРазделить("7, 2, 4, 10, 3, 5, 9, 4, 6, 8", ",");
		ВесовыеКоэффициенты2 = СтрРазделить("3, 7, 2, 4, 10, 3, 5, 9, 4, 6, 8", ",");
	Иначе
		ИННМассив = Новый Массив(10);
		ВесовыеКоэффициенты1 = СтрРазделить("2, 4, 10, 3, 5, 9, 4, 6, 8", ",");
		ВесовыеКоэффициенты2 = Новый Массив;
	КонецЕсли;

	Если КодРегиона = "00" Или СтрДлина(КодРегиона) <> 2 Тогда
		ИННМассив.Установить(0, СлучайноеЧисло(0, 9, 0));
		ИННМассив.Установить(1, СлучайноеЧисло(0, 9, 0));
	Иначе
		ИННМассив.Установить(0, Число(Сред(КодРегиона, 1, 1)));
		ИННМассив.Установить(1, Число(Сред(КодРегиона, 2, 1)));
	КонецЕсли;

	Для индекс = 2 По ИННМассив.Количество() - 1 Цикл
		ИННМассив.Установить(индекс, СлучайноеЧисло(0, 9, 0));
	КонецЦикла;

	Сумма1 = 0;
	Для индекс = 0 По ВесовыеКоэффициенты1.Количество() - 1 Цикл
		Элемент = ИННМассив.Получить(индекс) * Число(ВесовыеКоэффициенты1.Получить(индекс));
		Сумма1 = Сумма1 + Элемент;
	КонецЦикла;
	ИННМассив.Установить(ВесовыеКоэффициенты1.Количество(), Сумма1 % 11 % 10);

	Если ВесовыеКоэффициенты2.Количество() <> 0 Тогда
		Сумма2 = 0;
		Для индекс = 0 По ВесовыеКоэффициенты2.Количество() - 1 Цикл
			Элемент = ИННМассив.Получить(индекс) * Число(ВесовыеКоэффициенты2.Получить(индекс));
			Сумма2 = Сумма2 + Элемент;
		КонецЦикла;
		ИННМассив.Установить(ВесовыеКоэффициенты2.Количество(), Сумма1 % 11 % 10);
	КонецЕсли;

	СлучайныйИНН = СтрСоединить(ИННМассив);

	Возврат СлучайныйИНН;
КонецФункции

// Формирует случайный валидный КПП РФ
// 
// Параметры:
//  КодНалоговогоОргана - Строка - Код налогового органа, первые четыре цифры КПП. 
//  Причина - Число - Причина постановки. Поддерживаются следующие варианты
//    # 2 - код причины "43" постановка на учет филиала российской организации
//    # любая другая цифра - код причины "01" постановка на учет российской организации
//    по месту ее нахождения
// 
// Возвращаемое значение:
//  Строка - Случайный КПП, например 344401001
Функция СлучайныйКПП(КодНалоговогоОргана = "0000", Причина = 1) Экспорт

	СлучайныйКПП = "";

	Если ТипЗнч(Причина) = Тип("Число") И Причина = 2 Тогда
		Код2 = "43";
	Иначе
		Код2 = "01";
	КонецЕсли;

	Если ТипЗнч(КодНалоговогоОргана) = Тип("Строка") И СтрДлина(КодНалоговогоОргана) = 4 Тогда
		Код1 = КодНалоговогоОргана;
	Иначе
		МассивКод1 = Новый Массив(4);
		Для индекс = 0 По МассивКод1.ВГраница() Цикл
			МассивКод1.Установить(индекс, СлучайноеЧисло(0, 9, 0));
		КонецЦикла;
		Код1 = СтрСоединить(МассивКод1);
	КонецЕсли;
	
	Код3 = "00" + Строка(СлучайноеЧисло(0, 9, 0));
	
	СлучайныйКПП = СтрШаблон("%1%2%3", Код1, Код2, Код3);

	Возврат СлучайныйКПП;
КонецФункции

#КонецОбласти


#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Обработчик события "ПередКаждымТестом"
// 
// Параметры:
//  ОписаниеСобытия - см. ЮТФабрика.ОписаниеСобытияИсполненияТестов
Процедура ПередКаждымТестом(ОписаниеСобытия) Экспорт
	
	ЮТКонтекст.КонтекстТеста().Вставить("ВременныеФайлы", Новый Массив);
	
КонецПроцедуры

// Обработчик события "ПослеКаждогоТеста"
// 
// Параметры:
//  ОписаниеСобытия - см. ЮТФабрика.ОписаниеСобытияИсполненияТестов
Процедура ПослеКаждогоТеста(ОписаниеСобытия) Экспорт
	
	ВременныеФайлы = ЮТКонтекст.КонтекстТеста().ВременныеФайлы;
	
	Если ВременныеФайлы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Ошибки = Новый Массив();
	
	Для Каждого УдаляемыйФайл Из ВременныеФайлы Цикл
		
		Если ТипЗнч(УдаляемыйФайл) = Тип("Строка") Тогда
			УдаляемыйФайл = Новый Файл(УдаляемыйФайл);
		КонецЕсли;
		
		Попытка
			Если УдаляемыйФайл.Существует() Тогда
				
				Если УдаляемыйФайл.ПолучитьТолькоЧтение() Тогда
					УдаляемыйФайл.УстановитьТолькоЧтение(Ложь);
				КонецЕсли;
				
				УдалитьФайлы(УдаляемыйФайл.ПолноеИмя);
				
			КонецЕсли;
		Исключение
			
			Ошибки.Добавить(ЮТРегистрацияОшибок.ПредставлениеОшибки("Удаление файла " + УдаляемыйФайл, ИнформацияОбОшибке()));
			
		КонецПопытки;
		
	КонецЦикла;
	
	Если ЗначениеЗаполнено(Ошибки) Тогда
		ВызватьИсключение СтрСоединить(Ошибки, Символы.ПС);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьВарианты(Варианты, БазоваяСтруктура, ЗначенияПараметров, Ключи, Инд)
	
	Если Инд > Ключи.ВГраница() Тогда
		Возврат;
	КонецЕсли;
	
	Ключ = Ключи[Инд];
	Для Каждого Значение Из ЗначенияПараметров[Ключ] Цикл
		
		Вариант = ЮТОбщий.СкопироватьСтруктуру(БазоваяСтруктура);
		Вариант[Ключ] = Значение;
		Варианты.Добавить(Вариант);
		
		ДобавитьВарианты(Варианты, Вариант, ЗначенияПараметров, Ключи, Инд + 1);
		
	КонецЦикла;

КонецПроцедуры


#Область Бизнес_Данные

#КонецОбласти


#КонецОбласти
